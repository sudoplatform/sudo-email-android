buildscript {
    ext {
        agp_version = '8.5.0'
    }
    ext.kotlin_version = '1.9.24'
    ext.aws_version = '2.75.2'
    ext.appsync_version = '3.4.1'
    ext.dokka_version = '1.9.20'

    repositories {
        maven { url = "https://plugins.gradle.org/m2" }
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$agp_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.owasp:dependency-check-gradle:8.2.1"
        classpath "org.jmailen.gradle:kotlinter-gradle:3.16.0"
        classpath "com.amazonaws:aws-android-sdk-appsync-gradle-plugin:$appsync_version"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:4.0.0.2929"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"
        classpath "gradle.plugin.com.cookpad.android.plugin:plugin:1.2.8"
        classpath ("com.squareup.moshi:moshi") {
            version{
                strictly '1.8.0'
            }
        }
    }
}

plugins {
    id "io.github.gradle-nexus.publish-plugin" version "1.3.0"
    id "org.jetbrains.kotlin.plugin.serialization" version "1.9.23"
}

ext {
    projectGroup = 'com.sudoplatform'
}

if(project.file("${rootProject.projectDir}/util/internal-repo.gradle").exists()) {
    apply from: "${rootProject.projectDir}/util/internal-repo.gradle"
}
else {
    ext["internalRepoUsername"] = ""
    ext["internalRepoPassword"] = ""
    ext["internalSnapshotRepoUrl"] = ""
    ext["internalSnapshotRepoName"] = ""
    ext["internalReleaseRepoUrl"] = ""
    ext["internalReleaseRepoName"] = ""
    ext["sonatypeStagingProfileId"] = ""
}

if (project.hasProperty("ossIndexUsername")) {
    ext["ossIndexUsername"] = "$ossIndexUsername"
} else {
    ext["ossIndexUsername"] = ""
}

if (project.hasProperty("ossIndexPassword")) {
    ext["ossIndexPassword"] = "$ossIndexPassword"
} else {
    ext["ossIndexPassword"] = ""
}

if (project.findProperty("tag") && sonatypeStagingProfileId) {
    nexusPublishing {
        repositories {
            sonatype {
                stagingProfileId = sonatypeStagingProfileId
                username = nexusUsername
                password = nexusPassword
            }
        }
    }
}

allprojects {
    repositories {
        mavenLocal()
        google()
        mavenCentral()
        if (internalRepoUsername?.trim()) {
            maven {
                name = internalSnapshotRepoName
                url = internalSnapshotRepoUrl
                credentials {
                    username = internalRepoUsername
                    password = internalRepoPassword
                }
            }
            maven {
                name = internalReleaseRepoName
                url = internalReleaseRepoUrl
                credentials {
                    username = internalRepoUsername
                    password = internalRepoPassword
                }
            }
        }
    }
    gradle.projectsEvaluated {
        tasks.withType(Test) {
            outputs.upToDateWhen { false } // Force gradle to run unit tests even if everything is up to date
        }
    }
}

tasks.register('clean', Delete) {
    delete rootProject.buildDir
}
