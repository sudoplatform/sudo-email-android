buildscript {
    ext.kotlin_version = '1.4.30'
    ext.plugin_version = '2.2.0'
    ext.aws_version = '2.16.12'
    ext.appsync_version = '3.1.1'
    ext.dokka_version = '1.4.10'

    repositories {
        maven { url = "https://plugins.gradle.org/m2" }
        google()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.1.3"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.owasp:dependency-check-gradle:6.1.0"
        classpath "com.cookpad.android.licensetools:license-tools-plugin:1.7.0"
        classpath "org.jmailen.gradle:kotlinter-gradle:3.3.0"
        classpath "com.amazonaws:aws-android-sdk-appsync-gradle-plugin:$appsync_version"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:3.1.1"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.22.0"
    }
}

ext {
    projectGroup = 'com.sudoplatform'
}
apply plugin: 'io.codearte.nexus-staging'
nexusStaging {
    packageGroup = projectGroup
    stagingProfileId = '14314878e2a1b'
    numberOfRetries = 40
    delayBetweenRetriesInMillis = 5000
}

if(project.file("${rootProject.projectDir}/util/internal-repo.gradle").exists()) {
    apply from: "${rootProject.projectDir}/util/internal-repo.gradle"
}
else {
    ext["internalRepoUsername"] = ""
    ext["internalRepoPassword"] = ""
    ext["internalSnapshotRepoUrl"] = ""
    ext["internalSnapshotRepoName"] = ""
    ext["internalReleaseRepoUrl"] = ""
    ext["internalReleaseRepoName"] = ""
}

allprojects {
    repositories {
        mavenLocal()
        google()
        mavenCentral()
        if (internalRepoUsername?.trim()) {
            maven {
                name = internalSnapshotRepoName
                url = internalSnapshotRepoUrl
                credentials {
                    username = internalRepoUsername
                    password = internalRepoPassword
                }
            }
            maven {
                name = internalReleaseRepoName
                url = internalReleaseRepoUrl
                credentials {
                    username = internalRepoUsername
                    password = internalRepoPassword
                }
            }
        }
        jcenter()
    }
    gradle.projectsEvaluated {
        tasks.withType(Test) {
            outputs.upToDateWhen { false } // Force gradle to run unit tests even if everything is up to date
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
